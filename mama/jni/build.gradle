apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

def mamaVersion = project.findProperty('mamaVersion') ?: '6.2.2-1'
def mamaJniHeaderDir = project.findProperty('mamaJniHeaderDir') ?: 'mamajni'

if (!project.hasProperty('legacyNaming')) {
  group = "org.openmama"
  archivesBaseName = "openmama"
  version = mamaVersion
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile "junit:junit:4.12"  // Or whatever version
}

test { 
  environment "PATH", "C:\\Users\\fquinn\\SourceExternal\\github\\fquinner\\OpenMAMA\\openmama_install_6.2.1\\bin\\dynamic"
}

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.compile
    title = 'MAMA (Middleware Agnostic Messaging API) JNI specification, v' + mamaVersion
    options.overview = 'doc/overview-jni.html'
    options.windowTitle = 'MAMA JNI ' + mamaVersion
    options.bottom = 'Copyright 2011 NYSE Technologies'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

tasks.withType(JavaCompile) {
    options.compilerArgs += ["-h", mamaJniHeaderDir]
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'OpenMAMA'
        packaging 'jar'
        // optionally artifactId can be defined here 
        description 'OpenMAMA is a high performance vendor neutral lightweight wrapper that provides a common API interface to different middleware and messaging solutions across a variety of platforms and languages.'
        url 'https://www.openmama.org'

        scm {
          connection 'scm:https://github.com/OpenMAMA/OpenMAMA.git'
          developerConnection 'scm:git@github.com:OpenMAMA/OpenMAMA.git'
          url 'https://github.com/OpenMAMA/OpenMAMA'
        }

        licenses {
          license {
            name 'GNU LESSER GENERAL PUBLIC LICENSE 2.1'
            url 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.en.html'
          }
        }

        developers {
          developer {
            id 'fquinner'
            name 'Frank Quinn'
            email 'fquinn@cascadium.io'
          }
        }
      }
    }
  }
}